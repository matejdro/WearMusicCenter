apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: "dagger.hilt.android.plugin"
apply plugin: 'de.mobilej.unmock'

android {
    defaultConfig {
        applicationId "com.matejdro.wearmusiccenter"
        namespace = "com.matejdro.wearmusiccenter"
        compileSdk 35
        minSdkVersion 25
        targetSdkVersion 30
        versionCode 132
        versionName "1.9.3"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.ExperimentalStdlibApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }
    lintOptions {
        disable 'ExpiredTargetSdkVersion'
    }
}

kotlin {
    jvmToolchain(21)
}

unMock {
    keep "android.widget.BaseAdapter"
    keep "android.widget.ArrayAdapter"
    keep "android.os.Bundle"
    keepStartingWith "android.database.MatrixCursor"
    keep "android.database.AbstractCursor"
    keep "android.database.CrossProcessCursor"
    keepStartingWith "android.text.TextUtils"
    keepStartingWith "android.util."
    keepStartingWith "android.text."
    keepStartingWith "android.content.ContentValues"
    keepStartingWith "android.content.ComponentName"
    keepStartingWith "android.content.ContentUris"
    keepStartingWith "android.content.ContentProviderOperation"
    keepStartingWith "android.content.ContentProviderResult"
    keepStartingWith "android.content.UriMatcher"
    keepStartingWith "android.content.Intent"
    keep "android.location.Location"
    keepStartingWith "android.content.res.Configuration"
    keepStartingWith "org."
    keepStartingWith "libcore."
    keepStartingWith "com.android.internal.R"
    keepStartingWith "com.android.internal.util."
    keep "android.net.Uri"
    keep "android.view.KeyEvent"

    keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
}

dependencies {
    implementation project(path: ':wearutils')
    implementation project(path: ':common')

    implementation libs.google.wearableSupport
    compileOnly libs.google.wearableLibrary

    implementation libs.androidx.cardView
    implementation libs.androidx.appCompat
    implementation libs.androidx.core
    implementation libs.androidx.wear
    implementation libs.androidx.wear.ongoing
    implementation libs.androidx.fragment
    implementation libs.androidx.activity
    implementation libs.androidx.preference
    implementation libs.androidx.hilt.runtime
    implementation libs.androidx.lifecycle.service
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.livedata
    coreLibraryDesugaring(libs.desugarJdkLibs)
    kapt libs.androidx.hilt.compiler

    implementation libs.google.playServices.wearable

    implementation libs.kotlin.coroutines
    implementation libs.kotlin.coroutines.playServices

    testImplementation libs.junit4
    testImplementation libs.kotlin.coroutines.test
    unmock libs.unmock.androidSource
}
repositories {
    mavenCentral()
}
